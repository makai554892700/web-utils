group 'com.woaizhuangbi'
version '0.0.3-RELEASE'
archivesBaseName = 'spring-utils'
jar.enabled = true
bootJar.enabled = false
dependencies {
    compileOnly("org.springframework.boot:spring-boot-starter-web")
    compileOnly("org.springframework.boot:spring-boot-starter-data-jpa")
    compileOnly("javax.validation:validation-api:2.0.1.Final")
    // swagger3 依赖
    compileOnly("io.springfox:springfox-boot-starter:3.0.0")
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar, sourcesJar
}
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'SpringUtils'
                packaging 'jar'
                description 'A spring utils'
                url 'https://github.com/makai554892700/web-utils/tree/master/spring-utils'
                scm {
                    connection 'scm:git@github.com:makai554892700/web-utils.git'
                    developerConnection 'scm:git@github.com:makai554892700/web-utils.git'
                    url 'https://github.com/makai554892700/web-utils.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'makai'
                        name 'makai'
                        email 'makai554892700@sina.com'
                    }
                }
            }
        }
    }
}
signing {
    sign configurations.archives
}
// gradle spring-utils:uploadArchives
