group 'com.woaizhuangbi'
version '1.0.2-RELEASE'
archivesBaseName = 'spring-utils'
jar.enabled = true
bootJar.enabled = false
dependencies {
    compileOnly("org.springframework.boot:spring-boot-starter-web")
    compileOnly("org.springframework.boot:spring-boot-starter-data-jpa")
    compileOnly("org.hibernate.validator:hibernate-validator:6.0.18.Final")
    // swagger3 依赖
    compileOnly("io.springfox:springfox-boot-starter:3.0.0")
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'SpringUtils'
                packaging = 'jar'
                description = 'A spring utils'
                url = 'https://github.com/makai554892700/web-utils/tree/master/spring-utils'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'makai'
                        name = 'makai'
                        email = 'makai554892700@sina.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:makai554892700/web-utils.git'
                    developerConnection = 'scm:git@github.com:makai554892700/web-utils.git'
                    url = 'https://github.com/makai554892700/web-utils.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
signing {
    sign publishing.publications.mavenJava
}
javadoc {
    options.addStringOption("charset", "UTF-8")
}
// gradle spring-utils:publish
