group 'com.woaizhuangbi'
version '1.0.1-RELEASE'
archivesBaseName = 'minio-utils'
jar.enabled = true
bootJar.enabled = false
dependencies {
    compileOnly("com.woaizhuangbi:spring-utils:1.0.0-RELEASE")
    compileOnly("com.woaizhuangbi:file-interface:1.0.0-RELEASE")
    compileOnly("org.springframework.boot:spring-boot-starter-web")
    implementation("io.minio:minio:3.0.10")
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'minio-utils'
                packaging = 'jar'
                description = 'A minio utils'
                url = 'https://github.com/makai554892700/web-utils/tree/master/minio-utils'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'makai'
                        name = 'makai'
                        email = 'makai554892700@sina.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:makai554892700/web-utils.git'
                    developerConnection = 'scm:git@github.com:makai554892700/web-utils.git'
                    url = 'https://github.com/makai554892700/web-utils.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
signing {
    sign publishing.publications.mavenJava
}
javadoc {
    options.addStringOption("charset", "UTF-8")
}
// gradle minio-utils:publish
